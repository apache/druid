#!/usr/bin/env perl

# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.

use strict;
use warnings;
use File::Basename;

sub fail_check {
  my ($current_version) = @_;
  my $current_version_text = $current_version
    ? "Your current version is: $current_version."
    : "No Java runtime was detected on your system.";

  print STDERR <<"EOT";
Druid requires Java 8 or 11. $current_version_text

If you believe this check is in error, or you want to proceed with a potentially
unsupported Java runtime, you can skip this check using an environment variable:

  export DRUID_SKIP_JAVA_CHECK=1

Otherwise, install Java 8 or 11 in one of the following locations.

  * DRUID_JAVA_HOME
  * JAVA_HOME
  * java (installed on PATH)

Other versions of Java versions may work, but are not officially supported.
EOT
  exit 1;
}

my $skip_var = $ENV{'DRUID_SKIP_JAVA_CHECK'};
if ($skip_var && $skip_var ne "0" && $skip_var ne "false" && $skip_var ne "f") {
  exit 0;
}

my $cwd =  dirname(__FILE__);
my $java_bin_dir = `. $cwd/java-util && get_java_bin_dir 2>&1`;

# If we could not find java
if ($java_bin_dir eq "") {
  fail_check()
}
my $java_exec = "${java_bin_dir}/java";
my $java_version = qx["$java_exec" -version 2>&1];
if ($?) {
  fail_check();
}

# If we know it won't work, die. Otherwise hope for the best.
if ($java_version =~ /version \"((\d+)\.(\d+).*?)\"/ && !($2 == 1 && $3 == 8) && $2 != 11 ) {
  fail_check($1);
}
